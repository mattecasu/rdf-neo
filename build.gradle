plugins {
    id 'java'
    id "io.freefair.lombok" version "8.10"
}

repositories {
    mavenCentral()
}

// only var-assignment method recognised by Dependabot,
// see https://github.com/dependabot/dependabot-core/issues/1618
def rdf4Version = "5.0.2"
def neo4jJavaVersion = "5.23.0"
def jUnitVersion = "5.10.3"
def slf4jVersion = "2.0.16"
def guavaVersion = "33.2.1-jre"
def commonsLangVersion = "3.16.0"
def jacksonDataFormatVersion = "2.17.2"

dependencies {

    // minimal rdf4j
    implementation "org.eclipse.rdf4j:rdf4j-model:${rdf4Version}"
    implementation "org.eclipse.rdf4j:rdf4j-rio-turtle:${rdf4Version}"

    implementation "org.neo4j.driver:neo4j-java-driver:${neo4jJavaVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonDataFormatVersion}"

    // JUnit 5
    testImplementation "org.junit.jupiter:junit-jupiter:${jUnitVersion}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

group = 'convert'
version = '1.0-SNAPSHOT'

tasks.register('run', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = project.hasProperty("mainClass") ? project.getProperty("mainClass") : "convert.RdfToNeoImporter"
}

tasks.named('test', Test) {
    useJUnitPlatform()
}
